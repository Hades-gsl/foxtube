plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.openapi.generator" version "7.6.0"
    id "io.freefair.lombok" version "8.6"
}

group = 'com.hades'
version = '1.0.0'

def genSrc = "$rootDir/build/generated"
def resourcesSrc = "$rootDir/src/main/resources"

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.17.1'
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.17.1'
    // https://mvnrepository.com/artifact/io.swagger.core.v3/swagger-annotations
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.21'
    // https://mvnrepository.com/artifact/jakarta.validation/jakarta.validation-api
    implementation group: 'jakarta.validation', name: 'jakarta.validation-api', version: '3.1.0-M2'
    // https://mvnrepository.com/artifact/org.openapitools/jackson-databind-nullable
    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.6'
    // https://mvnrepository.com/artifact/org.mapstruct/mapstruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    // https://mvnrepository.com/artifact/org.mybatis.spring.boot/mybatis-spring-boot-starter
    implementation group: 'org.mybatis.spring.boot', name: 'mybatis-spring-boot-starter', version: '3.0.3'
    // https://mvnrepository.com/artifact/com.mysql/mysql-connector-j
    implementation group: 'com.mysql', name: 'mysql-connector-j', version: '8.4.0'
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '3.3.0'
    // https://mvnrepository.com/artifact/com.auth0/java-jwt
    implementation group: 'com.auth0', name: 'java-jwt', version: '4.4.0'
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-aop
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: '3.3.0'

    implementation platform('com.azure:azure-sdk-bom:1.2.24')
    implementation 'com.azure:azure-storage-blob'
    implementation 'com.azure:azure-storage-common'
    implementation 'com.azure:azure-identity'
}

tasks.named('test') {
    useJUnitPlatform()
}

openApiGenerate {
    generatorName.set('spring')
    outputDir.set("$genSrc")
    inputSpec.set("$resourcesSrc/openapi.json")
    packageName.set('com.hades.foxtube')
    apiPackage.set('com.hades.foxtube.api')
    modelPackage.set('com.hades.foxtube.dto')
    modelNameSuffix.set('Dto')
    configOptions.set([
            'interfaceOnly' : 'true',
            'useSpringBoot3': 'true',
    ])
    typeMappings.set([
            'integer': 'Long',
    ])
}

compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDirs += "$genSrc/src/main/java"

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named("bootBuildImage") {
    environment = [
            "BPE_DELIM_JAVA_TOOL_OPTIONS" : " ",
            "BPE_APPEND_JAVA_TOOL_OPTIONS" : "-XX:MaxDirectMemorySize=64m"
    ]
}